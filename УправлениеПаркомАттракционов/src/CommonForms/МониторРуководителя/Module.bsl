
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьДиаграммуПосещенияАттракционов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПосещенияАттракционовВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	
	Аттракцион = ЗначениеДиаграммы.Серия.Значение;
	День = ЗначениеДиаграммы.Точка.Значение;
	
	ПользовательскиеНастройки = НастройкиОтчетаДетализацияПосещений(Аттракцион, День);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ДеталтзацияПосещений.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьДиаграммуПосещенияАттракционов()
	
	ПосещенияАттракционов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПосещенияАттракционовОбороты.Аттракцион,
		|	ПосещенияАттракционовОбороты.КоличествоОборот КАК КоличествоПосещений,
		|	ПосещенияАттракционовОбороты.Период КАК День
		|ИЗ
		|	РегистрНакопления.ПосещенияАттракционов.Обороты(,, День,) КАК ПосещенияАттракционовОбороты";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Точка = ПосещенияАттракционов.УстановитьТочку(Выборка.День);
		Серия = ПосещенияАттракционов.УстановитьСерию(Выборка.Аттракцион);
		
		ПосещенияАттракционов.УстановитьЗначение(Точка, Серия, Выборка.КоличествоПосещений);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиОтчетаДетализацияПосещений(Аттракцион, День)
	
	Отчет = Отчеты.ДеталтзацияПосещений.Создать();
	ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	
	// Установка параметра период
	ПараметрПериод = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	ПользовательскийПараметрПериод = 
		ПользовательскиеНастройки.Элементы.Найти(ПараметрПериод.ИдентификаторПользовательскойНастройки);
		
	ЗначениеПараметраПериод = Новый СтандартныйПериод;
	ЗначениеПараметраПериод.ДатаНачала = НачалоДня(День);
	ЗначениеПараметраПериод.ДатаОкончания = КонецДня(День);	
	ПользовательскийПараметрПериод.Значение = ЗначениеПараметраПериод; 
	
	// Установка отбора Аттракцион
	ПолеДляОтбора = Новый ПолеКомпоновкиДанных("Аттракцион");
	Для Каждого ЭлементОбора Из Настройки.Отбор.Элементы Цикл
		
		Если ЭлементОбора.ЛевоеЗначение = ПолеДляОтбора Тогда
			
			ПользовательскийПараметрАттракцион = 
				ПользовательскиеНастройки.Элементы.Найти(ЭлементОбора.ИдентификаторПользовательскойНастройки);
			ПользовательскийПараметрАттракцион.Использование = Истина;
			ПользовательскийПараметрАттракцион.ПравоеЗначение = Аттракцион;
			
			Прервать;
				
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПользовательскиеНастройки;	
	
КонецФункции

#КонецОбласти
